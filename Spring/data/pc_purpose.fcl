
FUNCTION_BLOCK pc_purpose

VAR_INPUT               // ulazne varijable
	cpu_cores : REAL;
	vram_size: REAL;
	ram_size : REAL;
	disk_speed : REAL;
	disk_size : REAL;
END_VAR

VAR_OUTPUT              // izlazne varijable
	home_usage : REAL;
	professional_usage : REAL;
	server_hosting : REAL;
	gaming : REAL;
	mining : REAL;
END_VAR

FUZZIFY cpu_cores
	TERM low := (1, 1) (2, 1) (4, 0);
	TERM medium := (4, 1) (6, 1) (8, 0);
	TERM high := (8, 1) (12, 1) (16, 1);
END_FUZZIFY

FUZZIFY vram_size
	TERM low := (0, 0) (1024, 1) (2048, 1) (4096, 0);
	TERM medium := (2048, 0) (4096, 1) (6144, 1) (8192, 0);
	TERM high := (6144, 0) (8192, 1) (12288, 1) (16384, 0);
	TERM very_high := (12288, 0) (16384, 1);
END_FUZZIFY

FUZZIFY ram_size
	TERM low := (2, 1) (4, 0);
	TERM medium := (4, 0) (8, 1) (12, 1) (16, 0);
	TERM high := (12, 0) (16, 1) (32, 0);
	TERM very_high := (16, 0) (32, 0.8) (64, 1) (128, 1);
END_FUZZIFY

FUZZIFY disk_speed
	TERM low := (0, 1) (150, 1) (450, 1) (500, 0);
	TERM medium := (450, 0) (550, 1) (750, 1) (1800, 0);
	TERM high := (750, 0) (1800, 1) (7000, 1);
END_FUZZIFY

FUZZIFY disk_size
	TERM low := (128, 1) (250, 1) (500, 0); //GB
	TERM medium := (250, 0) (500, 1) (1024, 1) (2048, 1);
	TERM high := (2048, 0) (4096, 1) (8192, 1);
	TERM very_high := (6144, 0) (10240, 1) (14336, 1);
END_FUZZIFY

DEFUZZIFY home_usage
	TERM low := (0, 1) (2, 1) (4, 0);
	TERM medium := (3, 0) (5, 1) (7, 0);
	TERM high := (6, 0) (8, 1) (10, 1);
	METHOD : COG;		// 'Center Of Gravity' odn. centar mase (koristi se i MM odn. mean max)
	DEFAULT := 0;		// podrazumevana vrednost (ukoliko nijedno pravilo nije aktivirano)
END_DEFUZZIFY

DEFUZZIFY professional_usage
	TERM low := (0, 1) (2, 1) (4, 0);
	TERM medium := (3, 0) (5, 1) (7, 0);
	TERM high := (6, 0) (8, 1) (10, 1);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

DEFUZZIFY server_hosting
	TERM low := (0, 1) (2, 1) (4, 0);
	TERM medium := (3, 0) (5, 1) (7, 0);
	TERM high := (6, 0) (8, 1) (10, 1);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

DEFUZZIFY gaming
	TERM low := (0, 1) (2, 1) (4, 0);
	TERM medium := (3, 0) (5, 1) (7, 0);
	TERM high := (6, 0) (8, 1) (10, 1);
	METHOD : COG;
	DEFAULT := 0;		
END_DEFUZZIFY

DEFUZZIFY mining
	TERM low := (0, 1) (2, 1) (4, 0);
	TERM medium := (3, 0) (5, 1) (7, 0);
	TERM high := (6, 0) (8, 1) (10, 1);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK blok1
	AND : MIN;			// za konjunkciju se koristi minimum (samim tim sa za disjunkciju koristi maksimum)
	ACT : MIN;			// za zakljuƒçivanje (implikaciju) je izabran minimum (Mamdani implikacija) (osim MIN koristi se i PROD)
	ACCU : MAX;			// za kompoziciju je izabran maksimum (osim MAX koristi se i SUM)

	RULE 1 : IF cpu_cores IS low AND disk_speed IS low AND disk_size IS low AND ram_size IS low AND vram_size IS low THEN home_usage IS low;
	RULE 2 : IF vram_size IS very_high OR vram_size IS high THEN home_usage IS low;

	RULE 3 : IF cpu_cores IS low AND disk_speed IS medium AND disk_size IS medium AND ram_size IS medium AND vram_size IS low THEN home_usage IS medium;
	RULE 4 : IF cpu_cores IS medium AND disk_speed is low AND disk_size is low AND ram_size IS medium AND vram_size IS low THEN home_usage IS medium;

	RULE 5 : IF cpu_cores IS low AND disk_speed IS medium AND disk_size IS medium AND ram_size IS medium AND vram_size IS low THEN home_usage IS high;
	RULE 6 : IF cpu_cores IS medium AND disk_speed IS low AND disk_size IS low AND ram_size IS medium AND vram_size IS medium THEN home_usage IS high;

END_RULEBLOCK

RULEBLOCK blok2
	AND : MIN;
	ACT : MIN;
	ACCU : MAX;

	RULE 1 : IF cpu_cores IS low THEN professional_usage IS low;
	RULE 2 : IF ram_size IS low OR ram_size IS very_high THEN professional_usage IS low;
	RULE 3 : IF cpu_cores IS medium AND ram_size IS low AND disk_speed IS low THEN professional_usage IS low;

	RULE 4 : IF cpu_cores IS medium AND ram_size IS very_high AND disk_speed IS low THEN professional_usage IS medium;
	RULE 5 : IF cpu_cores IS medium AND ram_size IS high AND disk_speed IS low THEN professional_usage IS medium;
	RULE 6 : IF cpu_cores IS high AND ram_size IS medium AND disk_speed IS medium THEN professional_usage IS medium;
	RULE 7 : IF cpu_cores IS medium AND ram_size IS medium AND disk_speed IS medium THEN professional_usage IS medium;
	RULE 8 : IF cpu_cores IS high AND ram_size IS high AND disk_speed IS medium THEN professional_usage IS medium;

	RULE 9 : IF cpu_cores IS medium AND ram_size IS medium AND disk_speed IS high THEN professional_usage IS high;
	RULE 10 : IF cpu_cores IS medium AND ram_size IS high AND disk_speed IS high THEN professional_usage IS high;
	RULE 11 : IF cpu_cores IS high AND ram_size IS medium AND disk_speed IS high THEN professional_usage IS high;
	RULE 12 : IF cpu_cores IS medium AND ram_size IS very_high AND disk_speed IS medium THEN professional_usage IS high;
	RULE 13 : IF cpu_cores IS high AND ram_size IS high AND disk_speed IS high THEN professional_usage IS high;

END_RULEBLOCK

RULEBLOCK blok3
	AND : MIN;
	ACT : MIN;
	ACCU : MAX;

	RULE 1 : IF cpu_cores IS low THEN server_hosting IS low;
	RULE 2 : IF ram_size IS low THEN server_hosting IS low;
	RULE 3 : IF cpu_cores IS medium AND ram_size IS medium AND disk_size IS medium THEN server_hosting IS low;
	RULE 4 : IF cpu_cores IS medium AND ram_size IS very_high AND disk_size IS low THEN server_hosting IS low;
	RULE 5 : IF cpu_cores IS medium AND ram_size IS high AND disk_size IS low THEN server_hosting IS low;
	RULE 6 : IF cpu_cores IS high AND ram_size IS medium AND disk_size is low THEN server_hosting IS low;

	RULE 7 : IF cpu_cores IS medium AND ram_size IS medium AND disk_size IS medium THEN server_hosting IS medium;
	RULE 8 : IF cpu_cores IS medium AND ram_size IS high AND disk_size IS medium THEN server_hosting IS medium;
	RULE 9 : IF cpu_cores IS medium AND ram_size IS very_high AND disk_size IS medium THEN server_hosting IS medium;
	RULE 10 : IF cpu_cores IS medium AND ram_size IS high AND disk_size IS high THEN server_hosting IS medium;
	RULE 11 : IF cpu_cores IS high AND ram_size IS high AND disk_size IS low THEN server_hosting IS medium;

	RULE 12 : IF cpu_cores IS high AND ram_size IS high AND disk_size IS high THEN server_hosting IS high;	
	RULE 13 : IF cpu_cores IS high AND ram_size IS very_high AND disk_size IS high THEN server_hosting IS high;
	RULE 14 : IF cpu_cores IS high AND ram_size IS very_high AND disk_size IS medium THEN server_hosting IS high;
	RULE 15 : IF cpu_cores IS high AND ram_size IS medium AND disk_size is high THEN server_hosting IS high;
	
END_RULEBLOCK

RULEBLOCK blok4
	AND : MIN;
	ACT : MIN;
	ACCU : MAX;

	RULE 1 : IF vram_size IS low THEN gaming IS low;
	RULE 2 : IF cpu_cores IS low THEN gaming IS low;
	RULE 3 : IF ram_size IS low THEN gaming IS low;

	RULE 4 : IF vram_size IS medium AND ram_size IS medium AND cpu_cores IS medium THEN gaming IS medium;
	RULE 5 : IF vram_size IS medium AND ram_size IS medium AND cpu_cores IS high THEN gaming IS medium;
	RULE 6 : IF vram_size IS medium AND ram_size IS very_high AND cpu_cores IS medium THEN gaming IS medium;
	RULE 7 : IF vram_size IS medium AND ram_size IS high AND cpu_cores IS medium THEN gaming IS medium;
	RULE 8 : IF vram_size IS high AND ram_size IS medium AND cpu_cores IS medium THEN gaming IS medium;
	RULE 9 : IF vram_size IS very_high AND ram_size IS medium AND cpu_cores IS medium THEN gaming IS medium;
	RULE 10 : IF vram_size IS medium AND ram_size IS medium AND cpu_cores IS high THEN gaming IS medium;

	RULE 11 : IF vram_size IS high AND ram_size IS high AND cpu_cores IS high THEN gaming IS high;
	RULE 12 : IF vram_size IS high AND ram_size IS medium AND cpu_cores IS high THEN gaming IS high;
	RULE 13 : IF vram_size IS high AND ram_size IS high AND cpu_cores IS medium THEN gaming IS high;
	RULE 14 : IF vram_size IS high AND ram_size IS very_high AND cpu_cores IS high THEN gaming IS high;
	RULE 15 : IF vram_size IS high AND ram_size IS very_high AND cpu_cores IS medium THEN gaming IS high;
	RULE 16 : IF vram_size IS very_high AND ram_size IS high AND cpu_cores IS high THEN gaming IS high;
	
END_RULEBLOCK

RULEBLOCK blok5
	AND : MIN;
	ACT : MIN;
	ACCU : MAX;

	RULE 1 : IF cpu_cores IS low THEN mining IS low;
	RULE 2 : IF ram_size IS low THEN mining IS low;
	RULE 3 : IF vram_size IS medium OR vram_size IS low THEN mining IS low;

	RULE 4 : IF vram_size IS high AND cpu_cores IS high AND ram_size IS medium THEN mining IS medium;
	RULE 5 : IF vram_size IS very_high AND cpu_cores IS medium AND ram_size IS medium THEN mining IS medium;
	RULE 6 : IF vram_size IS very_high AND cpu_cores IS medium AND ram_size IS very_high THEN mining IS medium;
	RULE 7 : IF vram_size IS very_high AND cpu_cores IS medium AND ram_size IS high THEN mining IS medium;

	RULE 8 : IF vram_size IS high AND cpu_cores IS high AND ram_size IS high THEN mining IS high;
	RULE 9 : IF vram_size IS very_high AND cpu_cores IS high AND ram_size IS high THEN mining IS high;
	RULE 10 : IF vram_size IS very_high AND cpu_cores IS high AND ram_size IS very_high THEN mining IS high;

END_RULEBLOCK

END_FUNCTION_BLOCK

